apply plugin: "maven-publish"
apply plugin: 'signing'

java {
    withJavadocJar()
    withSourcesJar()
}

def pomConfig = {
    developers {
        developer {
            id 'russell.bie'
            name 'Russell Bie'
            email 'russell.bie@disneystreaming.com'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) { publication ->
            artifactId="${rootProject.name}-${project.name}_${scalaBinaryVersion}"
            version="${sparkMinorVersion}_${project.version}"
            from components.java
            pom.withXml {
                def root = asNode()
                project.configurations.provided.allDependencies.each { dep ->
                    root.dependencies.'*'.last() + {
                        delegate.dependency {
                            delegate.groupId dep.group
                            delegate.artifactId dep.name
                            delegate.version dep.version
                            delegate.scope 'provided'
                        }
                    }
                }
                root.appendNode('description', 'A dependency injection framework for apache spark')
                root.children().last() + pomConfig
            }
        }
    }
    repositories {
        maven {
            
            afterEvaluate {
                url = project.getProperty("repo-url")
            }
            // fill in the empty strings with your CORP user name and password
            credentials {
                username project.getProperty("username")
                password project.getProperty("password")
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
