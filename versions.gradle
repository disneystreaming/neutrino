project.ext.scalaVersionMap = [
    '2.11': '2.11.12',
    '2.12': '2.12.13',
    '2.13': '2.13.5'
]

project.ext.sparkVersionMap = [
    '2.0': '2.0.2',
    '2.1': '2.1.3',
    '2.2': '2.2.3',
    '2.3': '2.3.3',
    '2.4': '2.4.6'
]

String getScalaBinaryVersion() {
    if (project.hasProperty("scala-version")) {
        def scalaBinaryVersion = project.property("scala-version")
        if (project.ext.scalaVersionMap.containsKey(scalaBinaryVersion)) {
            return scalaBinaryVersion
        } else {
            throw new RuntimeException("unknown scala version $scalaBinaryVersion")
        }
    } else {
        return '2.11'
    }
}

String getScalaVersion() {
    return project.ext.scalaVersionMap[getScalaBinaryVersion()]
}

String getSparkMinorVersion() {
    if (project.hasProperty("spark-version")) {
        def sparkMinorVersion = project.property("spark-version")
        if (project.ext.sparkVersionMap.containsKey(sparkMinorVersion)) {
            return sparkMinorVersion
        } else {
            throw new RuntimeException("unknown spark version $sparkMinorVersion")
        }
    } else {
        return '2.4'
    }
}

String getSparkVersion() {
    return project.ext.sparkVersionMap[getSparkMinorVersion()]
}

ext {
    neutrinoVersion = '0.3.3-SNAPSHOT'
    targetJavaVersion = '1.8'
    scalaVersion = getScalaVersion()
    scalaBinaryVersion = getScalaBinaryVersion()
    sparkMinorVersion = getSparkMinorVersion()
    sparkVersion = getSparkVersion()
    sparkTestVersion = "${getSparkVersion()}_0.14.0".toString()
    scalaTestVersion = '3.0.8'
    junitVersion = '4.12'
    slf4jVersion = '1.7.28'
    silencerVersion = '1.7.3'
    spotbugsVersion = '4.2.0'
    guavaVersion = '30.0-jre'
    scoveragePluginVersion = '5.0.0'
    guiceVersion = '4.2.3'
    scalaGuiceVersion = '4.2.11'
    scoverageVersion = '1.4.1'
    jacksonVersion = '2.12.6'
    kafkaClientVersion = '2.7.2 '
}
