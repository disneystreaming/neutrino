project.ext.scalaVersionMap = [
    '2.11': '2.11.12',
    '2.12': '2.12.13',
    '2.13': '2.13.5'
]

project.ext.sparkVersionMap = [
    '2.0': '2.0.2',
    '2.1': '2.1.3',
    '2.2': '2.2.3',
    '2.3': '2.3.3',
    '2.4': '2.4.8',
    '3.0': '3.0.3',
    '3.1': '3.1.3',
    '3.2': '3.2.1',
]

project.ext.sparkTestVersionMap = [
    '2.11': [
        '2.0': '2.0.2_0.14.0',
        '2.1': '2.1.3_0.14.0',
        '2.2': '2.2.3_0.14.0',
        '2.3': '2.3.3_0.14.0',
        '2.4': '2.4.5_0.14.0'
    ],
    '2.12': [
        '2.4': '2.4.7_1.1.1',
        '3.0': '3.0.2_1.1.1',
        '3.1': '3.1.2_1.1.1',
        '3.2': '3.2.0_1.1.1',
    ]
]


String getScalaBinaryVersion() {
    if (project.hasProperty("scala-version")) {
        def scalaBinaryVersion = project.property("scala-version")
        if (project.ext.scalaVersionMap.containsKey(scalaBinaryVersion)) {
            return scalaBinaryVersion
        } else {
            throw new RuntimeException("unknown scala version $scalaBinaryVersion")
        }
    } else {
        return '2.11'
    }
}

String getScalaVersion() {
    return project.ext.scalaVersionMap[getScalaBinaryVersion()]
}

String getSparkMinorVersion() {
    if (project.hasProperty("spark-version")) {
        def sparkMinorVersion = project.property("spark-version")
        if (project.ext.sparkVersionMap.containsKey(sparkMinorVersion)) {
            return sparkMinorVersion
        } else {
            throw new RuntimeException("unknown spark version $sparkMinorVersion")
        }
    } else {
        return '2.4'
    }
}

String getSparkVersion() {
    return project.ext.sparkVersionMap[getSparkMinorVersion()]
}

ext {
    neutrinoVersion = '0.3.3-SNAPSHOT'
    targetJavaVersion = '1.8'
    scalaVersion = getScalaVersion()
    scalaBinaryVersion = getScalaBinaryVersion()
    sparkMinorVersion = getSparkMinorVersion()
    sparkVersion = getSparkVersion()
    sparkTestVersion = project.ext.sparkTestVersionMap[getScalaBinaryVersion()][getSparkMinorVersion()].toString()
    scalaTestVersion = '3.0.8'
    junitVersion = '4.12'
    slf4jVersion = '1.7.28'
    silencerVersion = '1.7.8'
    spotbugsVersion = '4.5.3'
    guavaVersion = '30.0-jre'
    scoveragePluginVersion = '7.0.0'
    guiceVersion = '4.2.3'
    scalaGuiceVersion = '4.2.11'
    scoverageVersion = '1.4.11'
    jacksonVersion = '2.12.6'
    kafkaClientVersion = '2.7.2'
}
